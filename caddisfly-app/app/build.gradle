/*
 * Copyright (C) Stichting Akvo (Akvo Foundation)
 *
 * This file is part of Akvo Caddisfly.
 *
 * Akvo Caddisfly is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Akvo Caddisfly is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Akvo Caddisfly. If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'de.mobilej.unmock'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "org.akvo.caddisfly.external_camera"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 169
        versionName "External Camera - Alpha 2.0"
        resConfigs "en", "en_US", "fr", "in"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        applicationVariants.all { variant ->
            def productFlavor = variant.productFlavors[0] != null ? "${variant.productFlavors[0].name.capitalize()}" : ""
            def buildType = "${variant.buildType.name.capitalize()}"
            tasks["compile${productFlavor}${buildType}UnitTestSources"].dependsOn(tasks["merge${productFlavor}${buildType}Assets"])
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        caddisflyRelease {
            storeFile file(CADDISFLY_RELEASE_STORE_FILE)
            storePassword CADDISFLY_RELEASE_STORE_PASSWORD
            keyAlias CADDISFLY_RELEASE_KEY_ALIAS
            keyPassword CADDISFLY_RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.caddisflyRelease
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }

    sourceSets {
        main {
            res.srcDirs =
                    [
                            'src/main/res/layouts/main',
                            'src/main/res/layouts/sensor/liquid',
                            'src/main/res/layouts/sensor/strip',
                            'src/main/res/layouts/sensor/ec',
                            'src/main/res/layouts/diagnostic',
                            'src/main/res/layouts/general',
                            'src/main/res/layouts',
                            'src/main/res'
                    ]
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        return void
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }
}

unMock {
    keep "android.graphics.Color"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':openCVLibrary310')
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.3.0') {
        exclude group: 'com.android.support'
        exclude group: 'javax.inject'
    }
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.github.felHR85:UsbSerial:4.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.assertj:assertj-core:3.8.0'
    testImplementation 'org.robolectric:robolectric:4.4'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
apply plugin: 'com.android.application'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 29
    testOptions.unitTests.includeAndroidResources = true

    defaultConfig {
        applicationId "io.ffem"
        minSdkVersion 20
        //noinspection OldTargetApi
        targetSdkVersion 29
        versionCode 256
        versionName "Experiment"
        resConfigs "en", "en_US", "hi"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "default"

    productFlavors {
        soil {
            applicationIdSuffix ".soil"
            buildConfigField 'boolean', 'showExperimentalTests', 'false'
        }
        water {
            applicationIdSuffix ".water"
            buildConfigField 'boolean', 'showExperimentalTests', 'false'
        }
        tryout {
            applicationIdSuffix ".experiment"
            versionName "Experiment"
            buildConfigField 'boolean', 'showExperimentalTests', 'true'
        }
    }

    sourceSets {
        soil {
            java.srcDirs = ['src/mainExt/java', 'src/soil/java']
        }
        water {
            java.srcDirs = ['src/mainExt/java', 'src/water/java']
        }
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            debuggable true
            multiDexEnabled true
        }
    }

    signingConfigs {
        appRelease {
            storeFile file(FFEM_RELEASE_STORE_FILE)
            storePassword FFEM_RELEASE_STORE_PASSWORD
            keyAlias FFEM_RELEASE_KEY_ALIAS
            keyPassword FFEM_RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.appRelease
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        testOptions {
            unitTests.returnDefaultValues = true
        }
    }

    unMock {
        keep "android.graphics.Color"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.3.0-rc01'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference:1.1.1'

    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.0'

    // Math
    implementation 'org.apache.commons:commons-math3:3.6.1'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Architecture Components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Room
    implementation 'androidx.room:room-runtime:2.2.6'
    annotationProcessor 'androidx.room:room-compiler:2.2.6'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:27.0.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // Volley
    implementation 'com.android.volley:volley:1.2.0'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // Usb
    implementation 'com.github.felHR85:UsbSerial:6.1.0'

    implementation 'com.jjoe64:graphview:4.2.2'

    // Email
    implementation files('libs/activation.jar')
    implementation files('libs/additionnal.jar')
    implementation files('libs/mail.jar')

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.8.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.2"

    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.3.0') {
        exclude group: 'com.android.support'
        exclude group: 'javax.inject'
    }

    // Robolectric
    testImplementation 'org.robolectric:robolectric:4.5.1'

    // UIAutomator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    // Unmock
    unmock 'org.robolectric:android-all:4.3_r2-robolectric-0'

    // Firebase (Experiment Flavor)
    tryoutImplementation 'androidx.multidex:multidex:2.0.1'
    tryoutImplementation 'com.google.firebase:firebase-core:18.0.3'
    tryoutImplementation 'com.google.firebase:firebase-database:19.7.0'
    tryoutImplementation 'com.google.firebase:firebase-firestore:22.1.2'
    tryoutImplementation 'com.google.firebase:firebase-storage:19.2.2'
    tryoutImplementation 'com.google.firebase:firebase-auth:20.0.4'
}
//apply plugin: 'com.google.gms.google-services'
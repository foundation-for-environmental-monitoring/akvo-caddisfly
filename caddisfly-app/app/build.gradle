apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'de.mobilej.unmock'

def propertyFile = file(System.properties['user.home'] + '/.gradle/gradle.properties')
def properties = new Properties()
if (propertyFile.exists()) {
    properties.load(new FileInputStream(propertyFile))
}

android {
    compileSdkVersion 29
    testOptions.unitTests.includeAndroidResources = true

    defaultConfig {
        applicationId "io.ffem"
        minSdkVersion 19
        //noinspection OldTargetApi
        targetSdkVersion 29
        versionCode 261
        versionName "Beta 1.8"
        resConfigs "en", "en_US", "hi", "kn", "te"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "default"

    productFlavors {
        soil {
            applicationIdSuffix ".soil"
        }
        water {
            applicationIdSuffix ".water"
        }
    }

    sourceSets {
        soil {
            java.srcDirs = ['src/mainExt/java', 'src/soil/java']
        }
        water {
            java.srcDirs = ['src/mainExt/java', 'src/water/java']
        }
    }

    buildFeatures {
        dataBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        appRelease {
            if (properties.getProperty('FFEM_RELEASE_STORE_FILE')) {
                storeFile file(FFEM_RELEASE_STORE_FILE)
                storePassword FFEM_RELEASE_STORE_PASSWORD
                keyAlias FFEM_RELEASE_KEY_ALIAS
                keyPassword FFEM_RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            multiDexEnabled true
        }
        release {
            if (properties.getProperty('FFEM_RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.appRelease
            }
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" +
                    (System.currentTimeMillis()) + "L)"
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "ffem-${variant.flavorName}-${variant.versionCode}.apk"
                }
            }
        }
    }

    defaultConfig {
        // do not change these defaults
        buildConfigField "String", "SUPPORT_EMAIL", "\"info@ffem.io\""
        buildConfigField "Boolean", "TEST_RUNNING", "false"
        buildConfigField "Boolean", "TAKE_SCREENSHOTS", "false"
        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + 0L + ")"
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        testOptions {
            unitTests.returnDefaultValues = true
        }
    }

    kotlinOptions { jvmTarget = "1.8" }

    unMock {
        keep "android.graphics.Color"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference-ktx:1.1.1'

    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.0'

    // Math
    implementation 'org.apache.commons:commons-math3:3.6.1'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Architecture Components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Room
    implementation 'androidx.room:room-runtime:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'
    kapt 'android.arch.persistence.room:compiler:1.1.1'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'com.jjoe64:graphview:4.2.2'

    // Email
    implementation files('libs/activation.jar')
    implementation files('libs/additionnal.jar')
    implementation files('libs/mail.jar')

    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Testing
    testImplementation 'org.assertj:assertj-core:3.8.0'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.2'
    androidTestImplementation 'androidx.test:core-ktx:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'

    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.3.0') {
        exclude group: 'com.android.support'
        exclude group: 'javax.inject'
    }

    // Robolectric
    testImplementation 'org.robolectric:robolectric:4.4'

    // UIAutomator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    // Unmock
    unmock 'org.robolectric:android-all:4.3_r2-robolectric-0'

    debugImplementation 'androidx.multidex:multidex:2.0.1'

//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'
}
repositories {
    mavenCentral()
}
//apply plugin: 'com.google.gms.google-services'
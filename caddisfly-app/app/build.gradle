apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'de.mobilej.unmock'

def secretsFile = file(System.properties['user.home'] + '/.gradle/gradle.properties')
def secrets = new Properties()
if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
}

android {
    compileSdkVersion 29
    testOptions.unitTests.includeAndroidResources = true

    defaultConfig {
        applicationId "io.ffem"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 249
        versionName "Beta 1.3.9"
        resConfigs "en", "en_US", "hi"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "default"

    productFlavors {
        soil {
            applicationIdSuffix ".soil"
        }
        water {
            applicationIdSuffix ".water"
        }
    }

    sourceSets {
        soil {
            java.srcDirs = ['src/mainExt/java', 'src/soil/java']
        }
        water {
            java.srcDirs = ['src/mainExt/java', 'src/water/java']
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        appRelease {
            if (secrets.getProperty('FFEM_RELEASE_STORE_FILE')) {
                storeFile file(FFEM_RELEASE_STORE_FILE)
                storePassword FFEM_RELEASE_STORE_PASSWORD
                keyAlias FFEM_RELEASE_KEY_ALIAS
                keyPassword FFEM_RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            if (secrets.getProperty('FFEM_RELEASE_STORE_FILE')) {
                signingConfig signingConfigs.appRelease
            }
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" +
                    (System.currentTimeMillis()) + "L)"
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "ffem-${variant.flavorName}-${variant.versionCode}.apk"
                }
            }
        }
    }

    defaultConfig {
        // do not change these defaults
        buildConfigField "Boolean", "TEST_RUNNING", "false"
        buildConfigField "Boolean", "TAKE_SCREENSHOTS", "false"
        buildConfigField "String", "TEST_LANGUAGE", "\"en\""
        buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + 0L + ")"
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        testOptions {
            unitTests.returnDefaultValues = true
        }
    }

    unMock {
        keep "android.graphics.Color"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0-rc01'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.exifinterface:exifinterface:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.1'

    implementation 'com.simplecityapps:recyclerview-fastscroll:2.0.0'

    // Math
    implementation 'org.apache.commons:commons-math3:3.6.1'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Architecture Components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Room
    implementation 'androidx.room:room-runtime:2.2.5'
    kapt 'androidx.room:room-compiler:2.2.5'
    kapt 'android.arch.persistence.room:compiler:1.1.1'

    // Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'com.jjoe64:graphview:4.2.2'

    // Email
    implementation files('libs/activation.jar')
    implementation files('libs/additionnal.jar')
    implementation files('libs/mail.jar')

    // Testing
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.assertj:assertj-core:3.8.0'
    testImplementation 'androidx.test:core:1.2.0'

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation 'androidx.test:runner:1.2.0'

    // Espresso
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-beta01'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0-beta01'
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.3.0-beta01') {
        exclude group: 'com.android.support'
        exclude group: 'javax.inject'
    }

    // Robolectric
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation "com.github.nimbl3:robolectric.shadows-supportv4:4.1-SNAPSHOT"

    // UIAutomator
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'

    // Unmock
    unmock 'org.robolectric:android-all:4.3_r2-robolectric-0'

    implementation "androidx.core:core-ktx:1.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

//    implementation "androidx.preference:preference-ktx:1.1.0"
}
repositories {
    mavenCentral()
}
//apply plugin: 'com.google.gms.google-services'